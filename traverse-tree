#!/bin/zsh
#─────────────────────────────────────────────────────────────────
# Build a directory tree
#
# created: 2011-05-30
# author:  Ludovic Vigouroux <ludo@mundoludo.fr>
# Free to use, modify and redistribute.
#─────────────────────────────────────────────────────────────────

usage () {
	print "usage: traverse-tree [-q] config_file"
	exit 1
}

#─────────────────────────────────────────────────────────────────
# Retrieve directories hierarchy configuration
#─────────────────────────────────────────────────────────────────
parse_tree_config () {
	# read config command
	. $script_location/load-config names nodes tree $*
	# aliases
	rootname=${tree[root_name]}
	rootpath=${tree[root_location]}
	treelabel=${tree[root_label]}
	# default values
	[[ -z $rootpath ]] && rootpath=$HOME
	[[ -z $rootname ]] && rootname=${rootpath:t}
	[[ -z $treelabel ]] && treelabel=full
}

#─────────────────────────────────────────────────────────────────
# Launch three traversal
#─────────────────────────────────────────────────────────────────
init_tree () {
	# parameters
	typeset -Ag treepaths      # directories paths
	typeset -Ag treepathslabel # directories paths without converted names
	treepaths[$rootname]=$rootpath
	treepathslabel[$treelabel]=$rootpath

	# go !
	traverse_tree $treelabel $rootpath
}

#─────────────────────────────────────────────────────────────────
# Traverse directory tree
#
# ARGUMENT 1: label of the root
# ARGUMENT 2: path of the root
#─────────────────────────────────────────────────────────────────
traverse_tree() {
	typeset level=$1
	typeset localpath=$2

	# level is not a leaf and contain an array with childrens labels
	if [[ -n ${(P)level} && "array" == ${$(typeset +m $level)[1]} ]] ; then
		for label in ${(P)level}; do
			# Record path of the tree
			localname=${names[$label]:-$label}
			completepath=${localpath}/$localname
			treepaths[$localname]=$completepath
			treepathslabel[$label]=$completepath

			# Continue to traverse tree
			traverse_tree $label $completepath
		done
	fi
}

#─────────────────────────────────────────────────────────────────
# Load directories hierarchy
#─────────────────────────────────────────────────────────────────
# retrieve this file location
script_location=${0:A:h}

# retrieve tree config
parse_tree_config $*

# build directories tree
init_tree

quiet=false
local opt;
while getopts q opt; do
	case $opt in
		(q)
			quiet=true
			;;
		(\?)
			print Bad option, aborting.
			return 1
			;;
	esac
done

if ! $quiet; then
	for dirpath in ${(ov)treepaths}; do
		print $dirpath
	done
fi

# vim: set filetype=zsh:
