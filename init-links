#!/bin/zsh
#─────────────────────────────────────────────────────────────────
# Initialize links
#
# author:  Ludovic Vigouroux <ludo@mundoludo.fr>
# created: December 2011
# Free to use, modify and redistribute.
#─────────────────────────────────────────────────────────────────

create_links() {
	local location
	local dir

	for link in ${(ok)links}; do
		# retrieve file
		link_name=${link/\/*/}
		if [[ -n $links_location && -n $links_location[$link_name] ]]; then
			location=$treepathslabel[$links_location[$link_name]]
		else
			location=$link_path
		fi
		file=$location/$link
		link=$links[$link]

		# various checks
		if [[ ! -e $file ]]; then
				echo $file to link does not exists, aborting >&2
				exit 1
		fi
		if [[ -h $link ]]; then
			if [[ ${link:A} == $file ]]; then
				continue
			else
				echo $link "->" ${link:A} is different from $file, aborting >&2
				exit 1
			fi
		elif [[ -e $link ]]; then
			echo $link exists and is not a link, aborting >&2
			exit 1
		fi

		# create new link
		[[ ! -d ${link:h} ]] && mkdir -pv ${link:h}
		ln -sfnv $file $link
	done
}

# retrieve this file location
: ${script_location:=${0:A:h}}

# read config command
. $script_location/load-config links links_location $*

# load directories path
. $script_location/traverse-tree -q $*

link_path=$treepathslabel[etc]

create_links

# vim: set filetype=zsh:
